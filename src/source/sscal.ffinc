$BLOCK COMMENT --file sscal.3m_blas.man
NAME
   sscal(3f) - [BLAS:SINGLE_BLAS_LEVEL1] SX:=SA*SX.

SYNOPSIS
    SUBROUTINE SSCAL(N,SA,SX,INCX)

      .. Scalar Arguments ..
      REAL,intent(in)    :: SA
      INTEGER,intent(in) :: INCX,N
      ..
      .. Array Arguments ..
      REAL,intent(inout) :: SX(*)
      ..

DEFINITION

    SSCAL scales a vector by a constant.
    uses unrolled loops for increment equal to 1.
 
OPTIONS

  N
 
          N is INTEGER
         number of elements in input vector(s)
 
  SA
 
          SA is REAL
           On entry, SA specifies the scalar alpha.
 
 SX
 
          SX is REAL array, dimension ( 1 + ( N - 1 )*abs( INCX ) )
 
  INCX
 
          INCX is INTEGER
         storage spacing between elements of SX
 
AUTHORS

+ Univ. of Tennessee
+ Univ. of California Berkeley
+ Univ. of Colorado Denver
+ NAG Ltd.

 date:November 2017

 FURTHER DETAILS

     jack dongarra, linpack, 3/11/78.
     modified 3/93 to return if incx .le. 0.
     modified 12/3/93, array(1) declarations changed to array(*)
 
SEE ALSO
   Online html documentation available at
   http://www.netlib.org/lapack/explore-html/
$BLOCK END
!  =====================================================================
       SUBROUTINE SSCAL(N,SA,SX,INCX)
      implicit none
!
!  -- Reference BLAS level1 routine (version 3.8.0) --
!  -- Reference BLAS is a software package provided by Univ. of Tennessee,    --
!  -- Univ. of California Berkeley, Univ. of Colorado Denver and NAG Ltd..--
!     November 2017
!
!     .. Scalar Arguments ..
      REAL,intent(in)    :: SA
      INTEGER,intent(in) :: INCX,N
!     ..
!     .. Array Arguments ..
      REAL,intent(inout) :: SX(*)
!     ..
!
!  =====================================================================
!
!     .. Local Scalars ..
      INTEGER I,M,MP1,NINCX
!     ..
!     .. Intrinsic Functions ..
      INTRINSIC MOD
!     ..
      IF (N.LE.0 .OR. INCX.LE.0) RETURN
      IF (INCX.EQ.1) THEN
!
!        code for increment equal to 1
!
!
!        clean-up loop
!
         M = MOD(N,5)
         IF (M.NE.0) THEN
            DO I = 1,M
               SX(I) = SA*SX(I)
            enddo
            IF (N.LT.5) RETURN
         endif
         MP1 = M + 1
         DO I = MP1,N,5
            SX(I) = SA*SX(I)
            SX(I+1) = SA*SX(I+1)
            SX(I+2) = SA*SX(I+2)
            SX(I+3) = SA*SX(I+3)
            SX(I+4) = SA*SX(I+4)
         enddo
      ELSE
!
!        code for increment not equal to 1
!
         NINCX = N*INCX
         DO I = 1,NINCX,INCX
            SX(I) = SA*SX(I)
         enddo
      endif

      END SUBROUTINE SSCAL
