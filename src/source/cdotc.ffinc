$BLOCK COMMENT --file cdotc.3m_blas.man
NAME
   cdotc(3f) - [BLAS:COMPLEX_BLAS_LEVEL1] CDOTC := SUM CONJUGATE(CX) * CY (conjugated vector dot product)

SYNOPSIS

    COMPLEX FUNCTION CDOTC(N,CX,INCX,CY,INCY)

      .. Scalar Arguments ..
      INTEGER,intent(in) ::  INCX,INCY,N
      ..
      .. Array Arguments ..
      COMPLEX,intent(in) ::  CX(*),CY(*)
      ..

DEFINITION
 CDOTC forms the dot product of two complex vectors
 
      CDOTC = X^H * Y

OPTIONS

  N     number of elements in input vector(s)
  CX    array, dimension ( 1 + ( N - 1 )*abs( INCX ) )
  INCX  storage spacing between elements of CX
  CY    array, dimension ( 1 + ( N - 1 )*abs( INCY ) )
  INCY  storage spacing between elements of CY

AUTHORS
+ Univ. of Tennessee
+ Univ. of California Berkeley
+ Univ. of Colorado Denver
+ NAG Ltd.

 date:November 2017

 FURTHER DETAILS
     jack dongarra, linpack,  3/11/78.
     modified 12/3/93, array(1) declarations changed to array(*)

SEE ALSO
   Online html documentation available at
   http://www.netlib.org/lapack/explore-html/
$BLOCK END
!  =====================================================================
       COMPLEX FUNCTION CDOTC(N,CX,INCX,CY,INCY)
      implicit none
!
!  -- Reference BLAS level1 routine (version 3.8.0) --
!  -- Reference BLAS is a software package provided by Univ. of Tennessee,    --
!  -- Univ. of California Berkeley, Univ. of Colorado Denver and NAG Ltd..--
!     November 2017
!
!     .. Scalar Arguments ..
      INTEGER,intent(in) ::  INCX,INCY,N
!     ..
!     .. Array Arguments ..
      COMPLEX,intent(in) ::  CX(*),CY(*)
!     ..
!
!  =====================================================================
!
!     .. Local Scalars ..
      COMPLEX CTEMP
      INTEGER I,IX,IY
!     ..
!     .. Intrinsic Functions ..
      INTRINSIC CONJG
!     ..
      CTEMP = (0.0,0.0)
      CDOTC = (0.0,0.0)
      IF (N.LE.0) RETURN
      IF (INCX.EQ.1 .AND. INCY.EQ.1) THEN
!
!        code for both increments equal to 1
!
         DO I = 1,N
            CTEMP = CTEMP + CONJG(CX(I))*CY(I)
         enddo
      ELSE
!
!        code for unequal increments or equal increments
!          not equal to 1
!
         IX = 1
         IY = 1
         IF (INCX.LT.0) IX = (-N+1)*INCX + 1
         IF (INCY.LT.0) IY = (-N+1)*INCY + 1
         DO I = 1,N
            CTEMP = CTEMP + CONJG(CX(IX))*CY(IY)
            IX = IX + INCX
            IY = IY + INCY
         enddo
      endif
      CDOTC = CTEMP
      END FUNCTION CDOTC
