$BLOCK COMMENT --file dzasum.3m_blas.man
NAME
   dzasum.(3f) -- [BLAS:DOUBLE_BLAS_LEVEL1]

SYNOPSIS
    double precision function dzasum(n,zx,incx)

      .. Scalar Arguments ..
      integer,intent(in)                 :: incx,n
      ..
      .. Array Arguments ..
      complex(kind=real64),intent(inout) :: zx(*)
      ..

DEFINITION

    DZASUM takes the sum of the (|Re(.)| + |Im(.)|)'s of a complex vector and
    returns a double precision result.
 
OPTIONS

  N
 
          N is INTEGER
         number of elements in input vector(s)
 
 out] ZX
 
          ZX is complex(kind=real64) array, dimension ( 1 + ( N - 1 )*abs( INCX ) )
 
  INCX
 
          INCX is INTEGER
         storage spacing between elements of ZX
 
AUTHORS

+ Univ. of Tennessee
+ Univ. of California Berkeley
+ Univ. of Colorado Denver
+ NAG Ltd.

 date:November 2017

 FURTHER DETAILS

     jack dongarra, 3/11/78.
     modified 3/93 to return if incx .le. 0.
     modified 12/3/93, array(1) declarations changed to array(*)
 
SEE ALSO
   Online html documentation available at
   http://www.netlib.org/lapack/explore-html/
$BLOCK END
!  =====================================================================
       DOUBLE PRECISION FUNCTION DZASUM(N,ZX,INCX)
      implicit none
!
!  -- Reference BLAS level1 routine (version 3.8.0) --
!  -- Reference BLAS is a software package provided by Univ. of Tennessee,    --
!  -- Univ. of California Berkeley, Univ. of Colorado Denver and NAG Ltd..--
!     November 2017
!
!     .. Scalar Arguments ..
      INTEGER,intent(in)                 :: INCX,N
!     ..
!     .. Array Arguments ..
      complex(kind=real64),intent(inout) :: ZX(*)
!     ..
!
!  =====================================================================
!
!     .. Local Scalars ..
      DOUBLE PRECISION STEMP
      INTEGER I,NINCX
!     ..
!     .. External Functions ..  DOUBLE PRECISION DCABS1
!     ..
      DZASUM = 0.0d0
      STEMP = 0.0d0
      IF (N.LE.0 .OR. INCX.LE.0) RETURN
      IF (INCX.EQ.1) THEN
!
!        code for increment equal to 1
!
         DO I = 1,N
            STEMP = STEMP + DCABS1(ZX(I))
         enddo
      ELSE
!
!        code for increment not equal to 1
!
         NINCX = N*INCX
         DO I = 1,NINCX,INCX
            STEMP = STEMP + DCABS1(ZX(I))
         enddo
      endif
      DZASUM = STEMP
      END FUNCTION DZASUM
