$BLOCK COMMENT --file zdotc.3m_blas.man
NAME
   zdotc(3f) - [BLAS:COMPLEX16_BLAS_LEVEL1]

SYNOPSIS
    complex(kind=real64) FUNCTION ZDOTC(N,ZX,INCX,ZY,INCY)

      .. Scalar Arguments ..
      INTEGER,intent(in) :: INCX,INCY,N
      ..
      .. Array Arguments ..
      complex(kind=real64),intent(in) :: ZX(*),ZY(*)
      ..

DEFINITION

 ZDOTC forms the dot product of two complex vectors
      ZDOTC = X^H * Y

OPTIONS

  N
 
          N is INTEGER
         number of elements in input vector(s)
 
  ZX
 
          ZX is complex(kind=real64) array, dimension ( 1 + ( N - 1 )*abs( INCX ) )
 
  INCX
 
          INCX is INTEGER
         storage spacing between elements of ZX
 
  ZY
 
          ZY is complex(kind=real64) array, dimension ( 1 + ( N - 1 )*abs( INCY ) )
 
  INCY
 
          INCY is INTEGER
         storage spacing between elements of ZY
 
AUTHORS

+ Univ. of Tennessee
+ Univ. of California Berkeley
+ Univ. of Colorado Denver
+ NAG Ltd.

 date:November 2017

 FURTHER DETAILS

     jack dongarra, 3/11/78.
     modified 12/3/93, array(1) declarations changed to array(*)
 
SEE ALSO
   Online html documentation available at
   http://www.netlib.org/lapack/explore-html/
$BLOCK END
!  =====================================================================
pure complex(kind=real64) FUNCTION ZDOTC(N,ZX,INCX,ZY,INCY)
      implicit none
!
!  -- Reference BLAS level1 routine (version 3.8.0) --
!  -- Reference BLAS is a software package provided by Univ. of Tennessee,    --
!  -- Univ. of California Berkeley, Univ. of Colorado Denver and NAG Ltd..--
!     November 2017
!
!     .. Scalar Arguments ..
      INTEGER,intent(in) :: INCX,INCY,N
!     ..
!     .. Array Arguments ..
      complex(kind=real64),intent(in) :: ZX(*),ZY(*)
!     ..
!
!  =====================================================================
!
!     .. Local Scalars ..
      complex(kind=real64) :: ZTEMP
      INTEGER I,IX,IY
!     ..
!     .. Intrinsic Functions ..
      INTRINSIC DCONJG
!     ..
      ZTEMP = (0.0d0,0.0d0)
      ZDOTC = (0.0d0,0.0d0)
      IF (N.LE.0) RETURN
      IF (INCX.EQ.1 .AND. INCY.EQ.1) THEN
!
!        code for both increments equal to 1
!
         DO I = 1,N
            ZTEMP = ZTEMP + DCONJG(ZX(I))*ZY(I)
         enddo
      ELSE
!
!        code for unequal increments or equal increments
!          not equal to 1
!
         IX = 1
         IY = 1
         IF (INCX.LT.0) IX = (-N+1)*INCX + 1
         IF (INCY.LT.0) IY = (-N+1)*INCY + 1
         DO I = 1,N
            ZTEMP = ZTEMP + DCONJG(ZX(IX))*ZY(IY)
            IX = IX + INCX
            IY = IY + INCY
         enddo
      endif
      ZDOTC = ZTEMP

      END FUNCTION ZDOTC
