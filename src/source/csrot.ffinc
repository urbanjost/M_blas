$BLOCK COMMENT --file csrot.3m_blas.man
NAME
   csrot(3f) - [BLAS:COMPLEX_BLAS_LEVEL1] Applies a real Given's rotation to complex vectors.

SYNOPSIS

    SUBROUTINE CSROT( N, CX, INCX, CY, INCY, C, S )

      .. Scalar Arguments ..
      INTEGER,intent(in)       :: INCX, INCY, N
      REAL,intent(in)          :: C, S
      ..
      .. Array Arguments ..
      COMPLEX,intent(inout)    :: CX( * ), CY( * )
      ..

DEFINITION

 CSROT applies a plane rotation, where the cos and sin (c and s) are real
 and the vectors cx and cy are complex.
 jack dongarra, linpack, 3/11/78.
 
OPTIONS

  N
 
          N is INTEGER
           On entry, N specifies the order of the vectors cx and cy.
           N must be at least zero.
 
 CX
 
          CX is COMPLEX array, dimension at least
           ( 1 + ( N - 1 )*abs( INCX ) ).
           Before entry, the incremented array CX must contain the n
           element vector cx. On exit, CX is overwritten by the updated
           vector cx.
 
  INCX
 
          INCX is INTEGER
           On entry, INCX specifies the increment for the elements of
           CX. INCX must not be zero.
 
 CY
 
          CY is COMPLEX array, dimension at least
           ( 1 + ( N - 1 )*abs( INCY ) ).
           Before entry, the incremented array CY must contain the n
           element vector cy. On exit, CY is overwritten by the updated
           vector cy.
 
  INCY
 
          INCY is INTEGER
           On entry, INCY specifies the increment for the elements of
           CY. INCY must not be zero.
 
  C
 
          C is REAL
           On entry, C specifies the cosine, cos.
 
  S
 
          S is REAL
           On entry, S specifies the sine, sin.
 
AUTHORS

+ Univ. of Tennessee
+ Univ. of California Berkeley
+ Univ. of Colorado Denver
+ NAG Ltd.

 date:December 2016

SEE ALSO
   Online html documentation available at
   http://www.netlib.org/lapack/explore-html/
$BLOCK END
!  =====================================================================
       SUBROUTINE CSROT( N, CX, INCX, CY, INCY, C, S )
      implicit none
!
!  -- Reference BLAS level1 routine (version 3.7.0) --
!  -- Reference BLAS is a software package provided by Univ. of Tennessee,    --
!  -- Univ. of California Berkeley, Univ. of Colorado Denver and NAG Ltd..--
!     December 2016
!
!     .. Scalar Arguments ..
      INTEGER,intent(in)       :: INCX, INCY, N
      REAL,intent(in)          :: C, S
!     ..
!     .. Array Arguments ..
      COMPLEX,intent(inout)    :: CX( * ), CY( * )
!     ..
!
!  =====================================================================
!
!     .. Local Scalars ..
      INTEGER           I, IX, IY
      COMPLEX           CTEMP
!     ..
!     .. Executable Statements ..
!
      IF( N.LE.0 ) RETURN
      IF( INCX.EQ.1 .AND. INCY.EQ.1 ) THEN
!
!        code for both increments equal to 1
!
         DO I = 1, N
            CTEMP = C*CX( I ) + S*CY( I )
            CY( I ) = C*CY( I ) - S*CX( I )
            CX( I ) = CTEMP
         enddo
      ELSE
!
!        code for unequal increments or equal increments not equal
!          to 1
!
         IX = 1
         IY = 1
         IF( INCX.LT.0 ) IX = ( -N+1 )*INCX + 1
         IF( INCY.LT.0 ) IY = ( -N+1 )*INCY + 1
         DO I = 1, N
            CTEMP = C*CX( IX ) + S*CY( IY )
            CY( IY ) = C*CY( IY ) - S*CX( IX )
            CX( IX ) = CTEMP
            IX = IX + INCX
            IY = IY + INCY
         enddo
      endif

      END SUBROUTINE CSROT
