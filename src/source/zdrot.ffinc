$BLOCK COMMENT --file zdrot.3m_blas.man
NAME
   zdrot(3f) - [BLAS:COMPLEX16_BLAS_LEVEL1]

SYNOPSIS
    subroutine zdrot( n, zx, incx, zy, incy, c, s )

      .. Scalar Arguments ..
      integer,intent(in)          :: incx, incy, n
      double precision,intent(in) :: c, s
      ..
      .. Array Arguments ..
      complex(kind=real64),intent(inout) :: zx( * ), zy( * )
      ..

DEFINITION

 Applies a plane rotation, where the cos and sin (c and s) are real
 and the vectors cx and cy are complex.
 jack dongarra, linpack, 3/11/78.
 
OPTIONS

  N
 
          N is INTEGER
           On entry, N specifies the order of the vectors cx and cy.
           N must be at least zero.
 
 ZX
 
          ZX is complex(kind=real64) array, dimension at least
           ( 1 + ( N - 1 )*abs( INCX ) ).
           Before entry, the incremented array ZX must contain the n
           element vector cx. On exit, ZX is overwritten by the updated
           vector cx.
 
  INCX
 
          INCX is INTEGER
           On entry, INCX specifies the increment for the elements of
           ZX. INCX must not be zero.
 
 ZY
 
          ZY is complex(kind=real64) array, dimension at least
           ( 1 + ( N - 1 )*abs( INCY ) ).
           Before entry, the incremented array ZY must contain the n
           element vector cy. On exit, ZY is overwritten by the updated
           vector cy.
 
  INCY
 
          INCY is INTEGER
           On entry, INCY specifies the increment for the elements of
           ZY. INCY must not be zero.
 
  C
 
          C is DOUBLE PRECISION
           On entry, C specifies the cosine, cos.
 
  S
 
          S is DOUBLE PRECISION
           On entry, S specifies the sine, sin.
 
AUTHORS

+ Univ. of Tennessee
+ Univ. of California Berkeley
+ Univ. of Colorado Denver
+ NAG Ltd.

 date:December 2016

SEE ALSO
   Online html documentation available at
   http://www.netlib.org/lapack/explore-html/
$BLOCK END
!  =====================================================================
       subroutine zdrot( n, zx, incx, zy, incy, c, s )
      implicit none
!
!  -- Reference BLAS level1 routine (version 3.7.0) --
!  -- Reference BLAS is a software package provided by Univ. of Tennessee,    --
!  -- Univ. of California Berkeley, Univ. of Colorado Denver and NAG Ltd..--
!     December 2016
!
!     .. Scalar Arguments ..
      integer,intent(in)          :: incx, incy, n
      double precision,intent(in) :: c, s
!     ..
!     .. Array Arguments ..
      complex(kind=real64),intent(inout) :: zx( * ), zy( * )
!     ..
!
! =====================================================================
!
!     .. Local Scalars ..
      integer            i, ix, iy
      complex(kind=real64) ::         ctemp
!     ..
!     .. Executable Statements ..
!
      if( n.le.0 ) return
      if( incx.eq.1 .and. incy.eq.1 ) then
!
!        code for both increments equal to 1
!
         do i = 1, n
            ctemp = c*zx( i ) + s*zy( i )
            zy( i ) = c*zy( i ) - s*zx( i )
            zx( i ) = ctemp
         enddo
      else
!
!        code for unequal increments or equal increments not equal
!          to 1
!
         ix = 1
         iy = 1
         if( incx.lt.0 ) ix = ( -n+1 )*incx + 1
         if( incy.lt.0 ) iy = ( -n+1 )*incy + 1
         do i = 1, n
            ctemp = c*zx( ix ) + s*zy( iy )
            zy( iy ) = c*zy( iy ) - s*zx( ix )
            zx( ix ) = ctemp
            ix = ix + incx
            iy = iy + incy
         enddo
      endif

      end
