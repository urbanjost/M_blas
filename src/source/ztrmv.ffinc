$BLOCK COMMENT --file ztrmv.3m_blas.man
NAME
   ztrmv(3f) - [BLAS:COMPLEX_16_BLAS_LEVEL2]

SYNOPSIS
    subroutine ztrmv(uplo,trans,diag,n,a,lda,x,incx)

      .. Scalar Arguments ..
      integer,intent(in)                 :: incx,lda,n
      character,intent(in)               :: diag,trans,uplo
      ..
      .. Array Arguments ..
      complex(kind=real64),intent(in)    :: a(lda,*)
      complex(kind=real64),intent(inout) :: x(*)
      ..

DEFINITION

 ZTRMV performs one of the matrix-vector operations

    x := A*x,   or   x := A**T*x,   or   x := A**H*x,

 where x is an n element vector and A is an n by n unit, or non-unit,
 upper or lower triangular matrix.
 
OPTIONS

  UPLO
 
          UPLO is CHARACTER*1
           On entry, UPLO specifies whether the matrix is an upper or
           lower triangular matrix as follows:

              UPLO = 'U' or 'u'   A is an upper triangular matrix.

              UPLO = 'L' or 'l'   A is a lower triangular matrix.
 
  TRANS
 
          TRANS is CHARACTER*1
           On entry, TRANS specifies the operation to be performed as
           follows:

              TRANS = 'N' or 'n'   x := A*x.

              TRANS = 'T' or 't'   x := A**T*x.

              TRANS = 'C' or 'c'   x := A**H*x.
 
  DIAG
 
          DIAG is CHARACTER*1
           On entry, DIAG specifies whether or not A is unit
           triangular as follows:

              DIAG = 'U' or 'u'   A is assumed to be unit triangular.

              DIAG = 'N' or 'n'   A is not assumed to be unit
                                  triangular.
 
  N
 
          N is INTEGER
           On entry, N specifies the order of the matrix A.
           N must be at least zero.
 
  A
 
          A is complex(kind=real64) array, dimension ( LDA, N ).
           Before entry with UPLO = 'U' or 'u', the leading n by n
           upper triangular part of the array A must contain the upper
           triangular matrix and the strictly lower triangular part of
           A is not referenced.
           Before entry with UPLO = 'L' or 'l', the leading n by n
           lower triangular part of the array A must contain the lower
           triangular matrix and the strictly upper triangular part of
           A is not referenced.
           Note that when DIAG = 'U' or 'u', the diagonal elements of
           A are not referenced either, but are assumed to be unity.
 
  LDA
 
          LDA is INTEGER
           On entry, LDA specifies the first dimension of A as declared
           in the calling (sub) program. LDA must be at least
           max( 1, n ).
 
 X
 
          X is complex(kind=real64) array, dimension at least
           ( 1 + ( n - 1 )*abs( INCX ) ).
           Before entry, the incremented array X must contain the n
           element vector x. On exit, X is overwritten with the
           transformed vector x.
 
  INCX
 
          INCX is INTEGER
           On entry, INCX specifies the increment for the elements of
           X. INCX must not be zero.
 
AUTHORS

+ Univ. of Tennessee
+ Univ. of California Berkeley
+ Univ. of Colorado Denver
+ NAG Ltd.

 date:December 2016

 FURTHER DETAILS

  Level 2 Blas routine.
  The vector and matrix arguments are not referenced when N = 0, or M = 0

  -- Written on 22-October-1986.
     Jack Dongarra, Argonne National Lab.
     Jeremy Du Croz, Nag Central Office.
     Sven Hammarling, Nag Central Office.
     Richard Hanson, Sandia National Labs.
 
SEE ALSO
   Online html documentation available at
   http://www.netlib.org/lapack/explore-html/
$BLOCK END
!  =====================================================================
subroutine ztrmv(uplo,trans,diag,n,a,lda,x,incx)
implicit none
!
!  -- Reference BLAS level2 routine (version 3.7.0) --
!  -- Reference BLAS is a software package provided by Univ. of Tennessee,    --
!  -- Univ. of California Berkeley, Univ. of Colorado Denver and NAG Ltd..--
!     December 2016
!
!     .. Scalar Arguments ..
      integer,intent(in)                 :: incx,lda,n
      character,intent(in)               :: diag,trans,uplo
!     ..
!     .. Array Arguments ..
      complex(kind=real64),intent(in)    :: a(lda,*)
      complex(kind=real64),intent(inout) :: x(*)
!     ..
!  =====================================================================
!
!     .. Parameters ..
      complex(kind=real64) :: zero
      parameter (zero= (0.0d+0,0.0d+0))
!     ..
!     .. Local Scalars ..
      complex(kind=real64) :: temp
      integer i,info,ix,j,jx,kx
      logical noconj,nounit
!     ..
!     .. External Functions ..
!      LOGICAL LSAME
!      EXTERNAL LSAME
!     ..
!     .. External Subroutines ..
!      EXTERNAL XERBLA
!     ..
!     .. Intrinsic Functions ..
      intrinsic dconjg,max
!     ..
!
!     Test the input parameters.
!
      info = 0
      if (.not.lsame(uplo,'U') .and. .not.lsame(uplo,'L')) then
          info = 1
      elseif (.not.lsame(trans,'N') .and. .not.lsame(trans,'T') .and.  .not.lsame(trans,'C')) then
          info = 2
      elseif (.not.lsame(diag,'U') .and. .not.lsame(diag,'N')) then
          info = 3
      elseif (n.lt.0) then
          info = 4
      elseif (lda.lt.max(1,n)) then
          info = 6
      elseif (incx.eq.0) then
          info = 8
      endif
      if (info.ne.0) then
          call xerbla('ZTRMV ',info)
          return
      endif
!
!     Quick return if possible.
!
      if (n.eq.0) return
!
      noconj = lsame(trans,'T')
      nounit = lsame(diag,'N')
!
!     Set up the start point in X if the increment is not unity. This
!     will be  ( N - 1 )*INCX  too small for descending loops.
!
      if (incx.le.0) then
          kx = 1 - (n-1)*incx
      elseif (incx.ne.1) then
          kx = 1
      endif
!
!     Start the operations. In this version the elements of A are
!     accessed sequentially with one pass through A.
!
      if (lsame(trans,'N')) then
!
!        Form  x := A*x.
!
          if (lsame(uplo,'U')) then
              if (incx.eq.1) then
                  do j = 1,n
                      if (x(j).ne.zero) then
                          temp = x(j)
                          do i = 1,j - 1
                              x(i) = x(i) + temp*a(i,j)
                          enddo
                          if (nounit) x(j) = x(j)*a(j,j)
                      endif
                  enddo
              else
                  jx = kx
                  do j = 1,n
                      if (x(jx).ne.zero) then
                          temp = x(jx)
                          ix = kx
                          do i = 1,j - 1
                              x(ix) = x(ix) + temp*a(i,j)
                              ix = ix + incx
                          enddo
                          if (nounit) x(jx) = x(jx)*a(j,j)
                      endif
                      jx = jx + incx
                  enddo
              endif
          else
              if (incx.eq.1) then
                  do j = n,1,-1
                      if (x(j).ne.zero) then
                          temp = x(j)
                          do i = n,j + 1,-1
                              x(i) = x(i) + temp*a(i,j)
                          enddo
                          if (nounit) x(j) = x(j)*a(j,j)
                      endif
                  enddo
              else
                  kx = kx + (n-1)*incx
                  jx = kx
                  do j = n,1,-1
                      if (x(jx).ne.zero) then
                          temp = x(jx)
                          ix = kx
                          do i = n,j + 1,-1
                              x(ix) = x(ix) + temp*a(i,j)
                              ix = ix - incx
                          enddo
                          if (nounit) x(jx) = x(jx)*a(j,j)
                      endif
                      jx = jx - incx
                  enddo
              endif
          endif
      else
!
!        Form  x := A**T*x  or  x := A**H*x.
!
          if (lsame(uplo,'U')) then
              if (incx.eq.1) then
                  do j = n,1,-1
                      temp = x(j)
                      if (noconj) then
                          if (nounit) temp = temp*a(j,j)
                          do i = j - 1,1,-1
                              temp = temp + a(i,j)*x(i)
                          enddo
                      else
                          if (nounit) temp = temp*dconjg(a(j,j))
                          do i = j - 1,1,-1
                              temp = temp + dconjg(a(i,j))*x(i)
                          enddo
                      endif
                      x(j) = temp
                  enddo
              else
                  jx = kx + (n-1)*incx
                  do j = n,1,-1
                      temp = x(jx)
                      ix = jx
                      if (noconj) then
                          if (nounit) temp = temp*a(j,j)
                          do i = j - 1,1,-1
                              ix = ix - incx
                              temp = temp + a(i,j)*x(ix)
                          enddo
                      else
                          if (nounit) temp = temp*dconjg(a(j,j))
                          do i = j - 1,1,-1
                              ix = ix - incx
                              temp = temp + dconjg(a(i,j))*x(ix)
                          enddo
                      endif
                      x(jx) = temp
                      jx = jx - incx
                  enddo
              endif
          else
              if (incx.eq.1) then
                  do j = 1,n
                      temp = x(j)
                      if (noconj) then
                          if (nounit) temp = temp*a(j,j)
                          do i = j + 1,n
                              temp = temp + a(i,j)*x(i)
                          enddo
                      else
                          if (nounit) temp = temp*dconjg(a(j,j))
                          do i = j + 1,n
                              temp = temp + dconjg(a(i,j))*x(i)
                          enddo
                      endif
                      x(j) = temp
                  enddo
              else
                  jx = kx
                  do j = 1,n
                      temp = x(jx)
                      ix = jx
                      if (noconj) then
                          if (nounit) temp = temp*a(j,j)
                          do i = j + 1,n
                              ix = ix + incx
                              temp = temp + a(i,j)*x(ix)
                          enddo
                      else
                          if (nounit) temp = temp*dconjg(a(j,j))
                          do i = j + 1,n
                              ix = ix + incx
                              temp = temp + dconjg(a(i,j))*x(ix)
                          enddo
                      endif
                      x(jx) = temp
                      jx = jx + incx
                  enddo
              endif
          endif
      endif

end subroutine ztrmv
